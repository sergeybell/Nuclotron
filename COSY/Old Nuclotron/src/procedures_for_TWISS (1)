PROCEDURE GETTWISS0 COORD AL0 BET0 GAM0 MAPARR SPNRARR;
VARIABLE NUM1 1;
VARIABLE NUM2 1;
IF COORD='X'; NUM1:=1;NUM2:=2;      AL0:=0; BET0:=10; GAM0:=0.1;
ELSEIF COORD='Y'; NUM1:=3;NUM2:=4;  AL0:=0; BET0:=10; GAM0:=0.1;
ELSEIF LO(1); WRITE 6 'UNKNOWN COORDINATE';
ENDIF;
FIT AL0 BET0 GAM0;
OBJ:= SQR((ME(NUM1,NUM1)^2)*BET0-2*ME(NUM1,NUM1)*ME(NUM1,NUM2)*AL0+(ME(NUM1,NUM2)^2)*GAM0-BET0); 
OBJ:=OBJ+ SQR(-ME(NUM1,NUM1)*ME(NUM2,NUM1)*BET0+( ME(NUM1,NUM1)*ME(NUM2,NUM2)+ME(NUM1,NUM2)*ME(NUM2,NUM1) )*AL0-ME(NUM1,NUM2)*ME(NUM2,NUM2)*GAM0-AL0);
OBJ:=OBJ+ SQR((ME(NUM2,NUM1)^2)*BET0-2*ME(NUM2,NUM1)*ME(NUM2,NUM2)*AL0+(ME(NUM2,NUM2)^2)*GAM0-(1+AL0^2)/BET0)  ;
ENDFIT 1E-20 5000 1 OBJ;
{WRITE 6 'ALPHA'&COORD&'0' 'BETA'&COORD&'0' 'GAMMA'&COORD&'0';
WRITE 6 AL0 BET0 GAM0;}
ENDPROCEDURE;

PROCEDURE BETS PATH TAG COORD NUM_ELE AL0 BET0 GAM0 BETS MAPARR SPNRARR LENARR;
VARIABLE J 1;
VARIABLE NUM1 1;
VARIABLE NUM2 1;
IF COORD='X'; NUM1:=1;NUM2:=2;
ELSEIF COORD='Y'; NUM1:=3;NUM2:=4;
ELSEIF LO(1); WRITE 6 'UNKNOWN COORDINATE';
ENDIF;
UM;BETS(1):=BET0;
OPENF 800 PATH&TAG 'REPLACE';
WRITE 800 ST(BETS(1))&'     '&ST(0);
LOOP J 1 NUM_ELE;
MAKEMAP 1 J 1 MAPARR1 SPNRARR1;
BETS(J+1):=(ME(NUM1,NUM1)^2)*BET0-2*ME(NUM1,NUM1)*ME(NUM1,NUM2)*AL0+(ME(NUM1,NUM2)^2)*GAM0;
WRITE 800 ST(BETS(J+1))&'     '&ST(LENARR(J));
UM;ENDLOOP;
CLOSEF 800;
{ WRITE 800 ST(MEAN)&' '&ST(SIGMA)&SF(NUM,'(I10)');} 
ENDPROCEDURE;


PROCEDURE GETDISP0 COORD D0 DPR0 MAPARR SPNRARR;
VARIABLE NUM1 1;
VARIABLE NUM2 1;
IF COORD='X'; NUM1:=1;NUM2:=2;      D0:=10; DPR0:=0;
ELSEIF COORD='Y'; NUM1:=3;NUM2:=4;  D0:=0; DPR0:=0;
ELSEIF LO(1); WRITE 6 'UNKNOWN COORDINATE';
ENDIF;
FIT D0 DPR0;
OBJ:= SQR(  ME(NUM1,NUM1)*D0+ME(NUM1,NUM2)*DPR0+ME(NUM1,6)*(1+1/(CONS(ETA)+1))-D0  ); 
OBJ:=OBJ+ SQR(  ME(NUM2,NUM1)*D0+ME(NUM2,NUM2)*DPR0+ME(NUM2,6)*(1+1/(CONS(ETA)+1))-DPR0  )  ;
ENDFIT 1E-20 5000 1 OBJ;
WRITE 6 'DISP'&COORD&'0' 'DISP_PRIME'&COORD&'0';
WRITE 6 D0 DPR0;
ENDPROCEDURE;

PROCEDURE DISPS PATH TAG COORD NUM_ELE D0 DPR0 DS MAPARR SPNRARR LENARR;
VARIABLE J 1;
VARIABLE NUM1 1;
VARIABLE NUM2 1;
IF COORD='X'; NUM1:=1;NUM2:=2;
ELSEIF COORD='Y'; NUM1:=3;NUM2:=4;
ELSEIF LO(1); WRITE 6 'UNKNOWN COORDINATE';
ENDIF;
UM;DS(1):=D0;
OPENF 800 PATH&TAG 'REPLACE';
WRITE 800 ST(DS(1))&'     '&ST(0);
LOOP J 1 NUM_ELE;
MAKEMAP 1 J 1 MAPARR1 SPNRARR1;
DS(J+1):=ME(NUM1,NUM1)*D0+ME(NUM1,NUM2)*DPR0+ ME(NUM1,6)*(  1+1/(CONS(ETA)+1)  );
WRITE 800 ST(DS(J+1))&'     '&ST(LENARR(J));
UM;ENDLOOP;
CLOSEF 800;
{ WRITE 800 ST(MEAN)&' '&ST(SIGMA)&SF(NUM,'(I10)');} 
ENDPROCEDURE;



{----------TWISS FUNCTIONS----------}
{
GETTWISS0 'X' ALPHAX0 BETAX0 GAMMAX0 MAPARR1 SPNRARR1;
GETTWISS0 'Y' ALPHAY0 BETAY0 GAMMAY0 MAPARR1 SPNRARR1;
BETS RESFOLDER 'BETAX' 'X' 267 ALPHAX0 BETAX0 GAMMAX0 BETAXS MAPARR1 SPNRARR1 LENGTHS;
BETS RESFOLDER 'BETAY' 'Y' 267 ALPHAY0 BETAY0 GAMMAY0 BETAYS MAPARR1 SPNRARR1 LENGTHS;
}
{
GETDISP0 'X' DISPX0 DISPXPR0 MAPARR1 SPNRARR1;
GETDISP0 'Y' DISPY0 DISPYPR0 MAPARR1 SPNRARR1;
DISPS RESFOLDER 'DISPX' 'X' 267 DISPX0 DISPXPR0 DISPXS MAPARR1 SPNRARR1 LENGTHS;
DISPS RESFOLDER 'DISPY' 'Y' 267 DISPY0 DISPYPR0 DISPYS MAPARR1 SPNRARR1 LENGTHS;
}
