INCLUDE 'seq_Nuclotron_Mod';

PROCEDURE MCMCALC ALPHA0 ALPHA1;
      VARIABLE TMPALPHA 100;
      VARIABLE I 1;
      VARIABLE MCM0 1; VARIABLE MCM1 1; VARIABLE MCM2 1; VARIABLE MCM3 1; VARIABLE MCM4 1;

      MCM MAP SPOS TMPALPHA;
      MCM0 := -TMPALPHA|(0&0&0&0&0&0);
      MCM1 := -TMPALPHA|(0&0&0&0&0&1);
      MCM2 := -2*TMPALPHA|(0&0&0&0&0&2);
      MCM3 := -2*3*TMPALPHA|(0&0&0&0&0&3);
      MCM4 := -2*3*4*TMPALPHA|(0&0&0&0&0&4);

      ALPHA0:= MCM1; ALPHA1:= MCM2;

      WRITE 6 '++++++++ MOMENTUM COMPACTION FACTOR ++++++++';
      WRITE 6 'MC0 = '&ST(MCM0);
      WRITE 6 'MC1 = '&ST(MCM1);
      WRITE 6 'MC2 = '&ST(MCM2);
      WRITE 6 'MC3 = '&ST(MCM3); WRITE 6 'MC4 = '&ST(MCM4);
      WRITE 6 '++++++++++++++++++++++++++++++++++++++++++++';

ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE I 1; VARIABLE J 1;
  VARIABLE GAMMA 1; VARIABLE BETA 1;
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  VARIABLE MU_N_ARR 5000 1000; VARIABLE MU_TP 800 3; VARIABLE MU0 1;
  VARIABLE ALPHA0 1; VARIABLE ALPHA1 1; VARIABLE ALPHA2 1;
  VARIABLE ETA0 1; VARIABLE ETA1 1; VARIABLE VALUE0 1; VARIABLE VALUE1 1;
  VARIABLE CHROM_X 1; VARIABLE CHROM_Y 1; VARIABLE quadKx 1; VARIABLE quadKy 1; VARIABLE quadKz 1;
  VARIABLE Nx 1; VARIABLE Ny 1; VARIABLE Nz 1;
  VARIABLE EB1 1;
  VARIABLE SGx1 1; VARIABLE SGy1 1; VARIABLE SGx2 1; VARIABLE SGy2 1;
  VARIABLE TILT 48; VARIABLE NTURN 1;
  VARIABLE MODE 100; VARIABLE FRINGE_FIELD 100;
  VARIABLE CASE 1;

  GAMMA := 1.143914; BETA := SQRT(1-1/(GAMMA*GAMMA));
  TILT := ZEROS(48);
  NTURN := 1;

  OV 3 3 0; {DAEPS 1E-12;}
  SET_FOR_DEUTERONS GAMMA;

  {WIEN FILTER}
  {EB1 := 132.685;} {16-period kV/cm}
  EB1 := 132.6727764948780;
  EB1 := 135;

  FRINGE_FIELD := 'OFF';  {ON & OFF FRINGE_FIELD}

  LOOP J 1 3;
    WRITE 6 '----------------------------SET MODE----------------------------';

    {INITIAL SEXTUPOLES}
    SGx1 := 0.0; SGy1 := 0.0; SGx2 := 0.0; SGy2 := 0.0;

    {START SEXTUPOLES}
    {SGx1 := 0.3660990130292385E-001;}
    {SGx1 := 0.4575E-001;}
    {SGy1 := -.5458494419716226E-001;
    SGx2 := -.2E-001;}

    {X, Y, D modes are possible}
    IF     J=1; WRITE 6 'X-MODE'; MODE := 'X';
    ELSEIF J=2; WRITE 6 'Y-MODE'; MODE := 'Y';
    ELSEIF J=3; WRITE 6 'D-MODE'; MODE := 'D';
    ENDIF;

    {Prepace files for data}
    OPENF 772 'dat/SCvsBC/'&ST(MODE)&'/Params_'&ST(FRINGE_FIELD)&'.dat' 'OLD';
    WRITE 772 'I SGx1 SGy1 SGx2 EB1 CHROM_X CHROM_Y ALPHA0 ALPHA1 ETA1';

    OPENF 773 'dat/SCvsBC/'&ST(MODE)&'/TSS_analysis_'&ST(FRINGE_FIELD)&'.dat' 'OLD';
    WRITE 773 'I CONS(MU) CONS(NBAR(1)) CONS(NBAR(2)) CONS(NBAR(3)) quadKx quadKy quadKz';

    OPENF 774 'dat/SCvsBC/'&ST(MODE)&'/Particles_'&ST(FRINGE_FIELD)&'.dat' 'OLD';
    WRITE 774 'I SPIN-TUNE(1) SPIN-TUNE(2) SPIN-TUNE(3)';

    LOOP I 1 30;
      WRITE 6 '----------------------------ANOTHER LOOP----------------------------';
      CASE := MRK(I);
      {Lattice}
      IF FRINGE_FIELD='ON';      WRITE 6 'FRINGE_FIELD ON'; FR 3;
      ELSEIF FRINGE_FIELD='OFF'; WRITE 6 'FRINGE_FIELD OFF';
      ENDIF;
      LATTICE SGx1 SGy1 SGx2 SGy2 EB1 1 TILT 0; WRITE 6 'LATTICE ALREADY READ';
      {PM 6;}

      {Paticles w different initial parameters}
      CR;
      IF MODE='X';
        WRITE 6 'X-plane';
        SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
        SR 1*1E-3 0 0 0 0 0 0 0 1; SSR 0 0 1;
        SR 5*1E-3 0 0 0 0 0 0 0 1; SSR 0 0 1;
        SR 10*1E-3 0 0 0 0 0 0 0 1; SSR 0 0 1;
      ELSEIF MODE='Y';
        WRITE 6 'Y-plane';
        SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
        SR 0 0 1*1E-3 0 0 0 0 0 1; SSR 0 0 1;
        SR 0 0 5*1E-3 0 0 0 0 0 1; SSR 0 0 1;
        SR 0 0 10*1E-3 0 0 0 0 0 1; SSR 0 0 1;
      ELSEIF MODE='D';
        WRITE 6 'D-plane';
        SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
        SR 0 0 0 0 0 1/10*1E-3 0 0 1; SSR 0 0 1;
        SR 0 0 0 0 0 5/10*1E-3 0 0 1; SSR 0 0 1;
        SR 0 0 0 0 0 10/10*1E-3 0 0 1; SSR 0 0 1;
      ENDIF;

      {Spin tune calc}
      TSS MU NBAR 0; GET_TUNE_ENSEMBLE MU_N_ARR;

      {Quad coefficients}
      MU0 := CONS(MU);
      DAPEE MU 11 quadKx;  DAPEE MU 33 quadKy;  DAPEE MU 66 quadKz;
      Nx := CONS(NBAR(1)); Ny := CONS(NBAR(2)); Nz := CONS(NBAR(3));

      MCMCALC ALPHA0 ALPHA1; {ALPHA Calc}
      ETA1 := ALPHA1+(3/2)*(BETA*BETA)/(GAMMA*GAMMA)-ALPHA0/(GAMMA*GAMMA)+1/(GAMMA*GAMMA*GAMMA*GAMMA);

      {write all sextupoles in FILE}
      OPENF 3618 'dat/ALPHA/SEXTUPOLES.dat' 'REPLACE';
        WRITE 3618 ST(SGx1); WRITE 3618 ST(SGy1); WRITE 3618 ST(SGx2); WRITE 3618 ST(SGy2);
      CLOSEF 3618;

      {Goto_CHROM file and calc Chromaticity for given SEXT}
      OS 'cosy Chrom.fox';
      OPENF 100500 'dat/ALPHA/Chrom.dat' 'OLD';
        READ 100500 VALUE0; READ 100500 VALUE1;
        CHROM_X := VALUE0;  CHROM_Y := VALUE1;
      CLOSEF 100500;

      {WRITE}
      WRITE 6 'SGx1, SGy1, EB1 = '&ST(SGx1)&' '&ST(SGy1)&' '&ST(EB1);
      WRITE 6 'CHROM_X, CHROM_Y = '&ST(CHROM_X)&' '&ST(CHROM_Y);
      WRITE 6 'quadKx, quadKy, quadKz = '&ST(quadKx)&' '&ST(quadKy)&' '&ST(quadKz);
      WRITE 6 'CONS(MU) = '&ST(CONS(MU));
      WRITE 6 'SPIN-TUNE = '&ST(MU_N_ARR(1)|3-CONS(MU));
      WRITE 6 'ALPHA0, ALPHA1 = '&ST(ALPHA0)&' '&ST(ALPHA1);

      WRITE 772 ST(I)&' '&ST(SGx1)&' '&ST(SGy1)&' '&ST(SGx2)&' '&ST(EB1)&' '&ST(CHROM_X)&' '&ST(CHROM_Y)&' '&ST(ALPHA0)&' '&ST(ALPHA1)&' '&ST(ETA1);
      WRITE 773 ST(I)&' '&ST(MU0)&' '&ST(Nx)&' '&ST(Ny)&' '&ST(Nz)&' '&ST(quadKx)&' '&ST(quadKy)&' '&ST(quadKz);
      WRITE 774 ST(I)&' '&ST(MU_N_ARR(1)|3-CONS(MU))&' '&ST(MU_N_ARR(1)|4-CONS(MU))&' '&ST(MU_N_ARR(1)|5-CONS(MU));

      {Prepace files for tracking data}
      OPENF 892 'dat/SCvsBC/'&ST(MODE)&'/TRPRAY_'&ST(FRINGE_FIELD)&'_'&CASE&'.dat' 'REPLACE';
      OPENF 893 'dat/SCvsBC/'&ST(MODE)&'/TRPSPI_'&ST(FRINGE_FIELD)&'_'&CASE&'.dat' 'REPLACE';
      OPENF 99  'dat/SCvsBC/'&ST(MODE)&'/PRAY_'&ST(FRINGE_FIELD)&'_'&CASE&'.dat' 'REPLACE';

      WRITE 6 '******************** STARTING TRACKING';
      PRAY 99; TRPRAY 892; TRPSPI 893;
      TR NTURN NINT(NTURN) -1 -3 1.2 1.2 0 0 -12;
      CLOSEF 99; CLOSEF 892; CLOSEF 893;

      {Set sextupoles for the next experement}
      IF MODE='X';
        SGx1 := SGx1+0.002; {SGx2 := SGx2+0.001;}
      ELSEIF MODE='Y';
        SGy1 := SGy1-0.002;
      ELSEIF MODE='D';
        SGx1 := SGx1+0.002;
      ENDIF;

    ENDLOOP;
    CLOSEF 772; CLOSEF 773; CLOSEF 774;
  ENDLOOP;
  ENDPROCEDURE; {MAIN}
PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
