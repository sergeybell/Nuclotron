{INCLUDE 'BYPASS_REAL_SEQ';}
INCLUDE 'BYPASS_REAL_4_fam_NEW';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-100E-3, 100E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    {SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;}
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE WHERE 100; VARIABLE MRKR 10;
  VARIABLE GAMMA 1; VARIABLE MAPSE 1;
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE EL 1;

  VARIABLE PNUM 1;
  VARIABLE PSI0_DEG 1;
  VARIABLE NTURN 1;
  VARIABLE SGx1 1; VARIABLE SGy1 1;
  VARIABLE SGx2 1; VARIABLE SGy2 1;
  VARIABLE EB1 1; VARIABLE EB2 1;

  GROUTF 'img/dump/TR' 1;
  GAMMA := 1.12792344429;
  NTURN := 1000;
  PSI0_DEG := 45;
  PNUM := 30;
  MRKR := 'NEW';

  OV 3 3 0;
  DAEPS 1E-12;
  { SET lattice parameters }
  SET_FOR_DEUTERONS GAMMA;

  {WIEN FILTER}
  {spin tune ~ 10^-4}
  {EB1 := 99.04;}
  {spin tune ~ 10^-7}
  {EB1 := 99.14849674631157;}
  EB1 := 0.0;

  {SEXTUPOLES}
  {natural chromaticity}
  {SGx1 :=  0.0;
  SGy1  := 0.0;
  SGx2 :=  0.0;
  SGy2  := 0.0;}

  {beta chromaticity}
  SGx1 := 0.1079811557830287E-001;
  SGy1 := -.1046954304378753E-001;
  SGx2 :=  0.0;
  SGy2  := 0.0;

  LATTICE SGx1 SGy1 SGx2 SGy2 EB1 0; PM 6;
  WRITE 6 '++++++++++ Magnetic Rigidity';
  WRITE 6 CONS(CHIM);

  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 'dat/'&MRKR&'/PRAY_'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 'dat/'&MRKR&'/DA_'&MRKR&'.dat' 'REPLACE';
  OPENF 893 'dat/'&MRKR&'/TRPSPI_'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN 1 -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
