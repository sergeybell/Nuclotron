{INCLUDE 'BYPASS_REAL_CORR';}
{INCLUDE 'BYPASS_REAL_3_fam';}
{INCLUDE 'BYPASS_REAL_4_fam';}
INCLUDE 'BYPASS_REAL_4_fam_NEW';

PROCEDURE MCMCALC ALPHA0 ALPHA1;
      VARIABLE TMPALPHA 100;
      VARIABLE I 1;
      VARIABLE MCM0 1;
      VARIABLE MCM1 1;
      VARIABLE MCM2 1;
      VARIABLE MCM3 1;
      VARIABLE MCM4 1;

      {L0:=183.4720631502572;}

      MCM MAP SPOS TMPALPHA;
      MCM0 := -TMPALPHA|(0&0&0&0&0&0);
      MCM1 := -TMPALPHA|(0&0&0&0&0&1);
      MCM2 := -2*TMPALPHA|(0&0&0&0&0&2);
      MCM3 := -2*3*TMPALPHA|(0&0&0&0&0&3);
      MCM4 := -2*3*4*TMPALPHA|(0&0&0&0&0&4);

      ALPHA0:= MCM1;
      ALPHA1:= MCM2;

      WRITE 6 '++++++++ MOMENTUM COMPACTION FACTOR ++++++++';
      WRITE 6 'MC0 = '&ST(MCM0);
      WRITE 6 'MC1 = '&ST(MCM1);
      WRITE 6 'MC2 = '&ST(MCM2);
      WRITE 6 'MC3 = '&ST(MCM3);
      WRITE 6 'MC4 = '&ST(MCM4);
      WRITE 6 '++++++++++++++++++++++++++++++++++++++++++++';

ENDPROCEDURE;

PROCEDURE OPTIMIZE_ENSEMBLE SEXTGx1 SEXTGy1 SEXTGx2 SEXTGy2 EBE;
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  VARIABLE OBJ1 1; VARIABLE OBJ2 1; VARIABLE OBJ 1;
                   VARIABLE SGx1 1; VARIABLE SGy1 1; VARIABLE EB1 1;
                   VARIABLE SGx2 1; VARIABLE SGy2 1;
                   VARIABLE E1 1;
                   VARIABLE GAMMA 1; VARIABLE BETA 1;
                   VARIABLE CHROM_X 1; VARIABLE CHROM_Y 1;
                   VARIABLE VALUE0 1; VARIABLE VALUE1 1;
                   VARIABLE quadKx 1; VARIABLE quadKy 1; VARIABLE quadKz 1; VARIABLE MU0 1;
                   VARIABLE MU_TP 800 3;
                   VARIABLE ALPHA0 1; VARIABLE ALPHA1 1;
                   VARIABLE ETA0 1; VARIABLE ETA1 1;
                   VARIABLE MU_N_ARR 5000 1000;

  WRITE 6 'BEGIN';
  SGx1 := SEXTGx1; SGy1 := SEXTGy1; EB1 := EBE;
  SGx2 := SEXTGx2; SGy2 := SEXTGy2;
  WRITE 6 'SGx1, SGy1, EB1 = '&ST(SGx1)&' '&ST(SGy1)&' '&ST(EB1);
  WRITE 6 'SGx2, SGy2 ='&ST(SGx2)&' '&ST(SGy2);
  GAMMA := 1.12792344429;
  BETA := SQRT(1-1/(GAMMA*GAMMA));
  FIT SGx1 SGy1 SGx2;
    {write all sextupoles in FILE}
    OPENF 3618 'dat/ALPHA/SEXTUPOLES.dat' 'REPLACE';
      WRITE 3618 ST(SGx1);
      WRITE 3618 ST(SGy1);
      WRITE 3618 ST(SGx2);
      WRITE 3618 ST(SGy2);
    CLOSEF 3618;
    OS 'cosy Chrom.fox';
    {read CHROM from calculated program}
    OPENF 100500 'dat/ALPHA/CHROM.dat' 'OLD';
      READ 100500 VALUE0;
      READ 100500 VALUE1;
      CHROM_X := VALUE0;
      CHROM_Y := VALUE1;
    CLOSEF 100500;

    LATTICE SGx1 SGy1 SGx2 SGy2 EB1 1;
    SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
    SR 1*1E-3 0 0 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 1*1E-3 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 0 0 0 1*1E-3 0 0 1; SSR 0 0 1;
    TSS MU NBAR 0;
    GET_TUNE_ENSEMBLE MU_N_ARR;
    MU0 := CONS(MU);
    DAPEE MU 11 quadKx;
    DAPEE MU 33 quadKy;
    DAPEE MU 66 quadKz;
    MCMCALC ALPHA0 ALPHA1;
    ETA1 := ALPHA1+(3/2)*(BETA*BETA)/(GAMMA*GAMMA)-ALPHA0/(GAMMA*GAMMA)+1/(GAMMA*GAMMA*GAMMA*GAMMA);
    OBJ := SQRT(SQR(MU_N_ARR(1)|3-CONS(MU))+SQR(MU_N_ARR(1)|4-CONS(MU))+SQR(MU_N_ARR(1)|5-CONS(MU)));
    WRITE 6 'SGx1, SGy1, EB1 = '&ST(SGx1)&' '&ST(SGy1)&' '&ST(EB1);
    WRITE 6 'SGx2, SGy2 ='&ST(SGx2)&' '&ST(SGy2);
    WRITE 6 'ALPHA1 = '&ST(ALPHA1);
    WRITE 6 'ETA1 = '&ST(ETA1);
    WRITE 6 'CHROM_X, CHROM_Y = '&ST(CHROM_X)&' '&ST(CHROM_Y);
    WRITE 6 'quadKx, quadKy, quadKz= '&ST(quadKx)&' '&ST(quadKy)&' '&ST(quadKz);
    WRITE 6 'MU0 = '&ST(MU0);
    WRITE 6 'OBJ ='&ST(OBJ);
    CR;
  ENDFIT 1E-13 2000 1 OBJ;

    WRITE 6 '====================================';
    WRITE 6 'SGx1, SGy1, EB1 = '&ST(SGx1)&' '&ST(SGy1)&' '&ST(EB1);
    WRITE 6 'SGx2, SGy2 ='&ST(SGx2)&' '&ST(SGy2);
    WRITE 6 'quadKx, quadKy, quadKz= '&ST(quadKx)&' '&ST(quadKy)&' '&ST(quadKz);
    WRITE 6 'MU0 = '&ST(MU0);
    WRITE 6 'OBJ ='&ST(OBJ);
    SEXTGx1 := SGx1; SEXTGy1 := SGy1; SEXTGx2 := SGx2; SEXTGy2 := SGy2;
ENDPROCEDURE; {OPTIMIZE}

PROCEDURE MAIN;
  VARIABLE GAMMA 1;
  VARIABLE SGx1 1; VARIABLE SGy1 1; VARIABLE EB1 1;
  VARIABLE SGx2 1; VARIABLE SGy2 1;
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  VARIABLE MU_N_ARR 5000 1000;

  GAMMA := 1.12792344429;
  OV 3 3 0;
  {DAEPS 1E-12;}
  { SET lattice parameters }
  SET_FOR_DEUTERONS GAMMA;

  {WIEN FILTER}
  {EB1 := 0.0;}
  {spin tune ~ 10^-4}
  EB1 := 99.04;
  {spin tune ~ 10^-7}
  {EB1 := 99.14849674631157;}
  {EB1 := 0.0;}

  {SEXTUPOLES}
  {natural chromaticity}

  SGx1 := 0.0;
  SGy1 := 0.0;
  SGx2 := 0.0;
  SGy2 := 0.0;

  OPTIMIZE_ENSEMBLE SGx1 SGy1 SGx2 SGy2 EB1;

  WRITE 6 'EB1 = '&ST(EB1);
  WRITE 6 'SGx1 := '&ST(SGx1)&';';
  WRITE 6 'SGy1 := '&ST(SGy1)&';';
  WRITE 6 'SGx2 := '&ST(SGx2)&';';
  WRITE 6 'SGy2 := '&ST(SGy2)&';';

  ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
